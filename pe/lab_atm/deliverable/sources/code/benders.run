reset;

model benders.mod
data atm.dat;

option solver cplex;

# Problem declaration (variables, Objective_Function, Restrictions)
problem Master: x, z, Total_Cost, Cuts;
problem Sub: u, Dual_Cost, MissingCost;

# Initializations
let NCuts := 0;
let X := L;
let z := C * X;

param GAP default Infinity;
param epsilon default 1.0e-8;

repeat {
    solve Sub;
    display Sub.result, Dual_Cost;

    # Check gap
    let GAP := abs(Dual_Cost - z);
    display GAP;
    if GAP <= epsilon * z then break;

    # Add cut
    let NCuts := NCuts + 1;
    let {i in S} Y[i, NCuts] := u[i];

    solve Master;
    display Master.result, x, z, Total_Cost;

    let X := x;
}

display x, Total_Cost;