
reset;

model cutting_plane.mod;
data cutting_plane.dat;
data caps.dat;
option solver cplex;


problem MasterProblem: 
    z, mu,                    # Variables
    cuts,                     # Restrictions
    Z;                        # Objective Function

problem SubProblem: 
    flow, tf, afo, afd,       # Variables
    I, AI, total_flow, caps, # Restrictions
    w;                        # Objective Function

param MAXCUTS default 1000;
let NCUTS := 1;
param epsilon = 1.0e-8;

# Generate Initial solution
let {f in FN, o in O} AFO[f, o, NCUTS] := T[o, o];
let {f in FN, n in N, o in O} AFD[f, n, o, NCUTS] := -T[n, o];

param MUA;
param VARIANT within {1..3} := 2;


for {1..MAXCUTS}{
    printf "Master Problem Iteration %i\n", NCUTS;
    solve MasterProblem;
    display MasterProblem.result;
    display z > "master.txt";

    if VARIANT = 1 || NCUTS = 1 then let {(i, j) in A} MU[i, j] := mu[i, j];
    else {
        if VARIANT = 2 then let MUA := 1 / (NCUTS);
        else let MUA := NCUTS^2 / sum{k in 1..NCUTS} k^2;
        printf "MUA = %f\n", MUA;
        
        let {(i, j) in A} MU[i, j] := MU[i, j] + MUA * (mu[i, j] - MU[i, j]);
    }
    printf "Sub Problem Iteration %i\n", NCUTS;
    solve SubProblem;
    display SubProblem.result;
    display w > "subproblem.txt";

    let NCUTS := NCUTS + 1;

    let {(i, j) in A} FLOW[i, j, NCUTS] := tf[i, j];
    let {f in FN, o in O} AFO[f, o, NCUTS] := afo[f, o];
    let {f in FN, i in N, o in O} AFD[f, i, o, NCUTS] := afd[f, i, o];

    if abs(w-z) < epsilon then {
        printf "Convergence reached in %i iterations\n", NCUTS;
        break;
    }
}

display w, z;
