reset;

model fw.mod;
data net.dat;
option solver minos;

# Iteration Params
param ITERATION;
param ALPHA;
param X {A};
param XL {A, O};
param GAP;
param OBJ_FUNC;

problem AoN: f, tf, I, flux_total, Gradient_F;  # SUBPROBLEM Definition.
problem Q: f, tf, I, flux_total, F; # Traffic Assignment (Equilibrium problem) Definition.


let {(i, j) in A} S[i, j] := 1;

solve AoN; 
let {(i, j) in A} X[i, j] := tf[i, j];
let {(i, j) in A, k in O} XL[i, j, k] := f[i, j, k];
display Gradient_F;

let ITERATION := 1;

param EPSILON := 0.0005;
param MAX_ITERATIONS := 1;

printf "Iteration,Objective Function,Relative Gap,Step Length\n" > "iterations/fw.csv";

repeat while ITERATION <= MAX_ITERATIONS {
	printf "------------------Iteration %d------------------\n", ITERATION;

	# Update Ck (1)
	let { (i, j) in A } S[i, j] := C[i, j] + DELTA * X[i, j];

	# display C, X, S;

	solve AoN;
	display Gradient_F;

	# Evaluate relative gap (2)
	let GAP :=
		  -(sum{(i, j) in A} S[i,j] * (tf[i,j] - X[i,j])) / 
		   (sum{(i, j) in A} S[i,j] * X[i,j]);
	printf "Relative Gap: %.2f\%\n", GAP * 100;

	# Line search (3)
	# -SUM(Sij(vij)'dij) / DELTA * SUM(dij^2) (For only linear boundary functions)
	let ALPHA := 
		- (sum { (i, j) in A } S[i, j] * (tf[i, j] - X[i, j])) /
		  (DELTA * (sum { (i, j) in A } (tf[i, j] - X[i, j])^2)); # COMPLETE
	let ALPHA := min(1, ALPHA);
	printf "Alpha: %.2e\n", ALPHA;

	# Update Xk (4)
	let {(i, j) in A} X[i, j] := X[i, j] + ALPHA * (tf[i, j] - X[i, j]);
	let {(i, j) in A, k in O} XL[i, j, k] := XL[i, j, k] + ALPHA * (f[i, j, k] - XL[i, j, k]);

	let OBJ_FUNC := sum{(i, j) in A} (C[i,j] * X[i,j] + 0.5 * DELTA * X[i, j]^2);
	printf "Objective Function: %.2e\n", OBJ_FUNC;

	printf "%d,%.4e,%.4e,%.4e\n", ITERATION, OBJ_FUNC, GAP, ALPHA > "iterations/fw.csv";

	if GAP < EPSILON then {
		break;
	}


	let ITERATION := ITERATION + 1;
}

display X;
solve Q;

option display_1col 200;
option omit_zero_rows 1;

for {(i, j) in A} {
	if abs(X[i, j] - sum{k in O} XL[i, j, k]) > 1e-4 then print "Error";
}

for {o in O} {
	for {i in N} {
		for {j in N} {
			if (i, j) not in A then continue;
			if XL[i, j, o] < 1e-6 then continue;
			printf "%d,%d,%d,%.2f\n", o, i, j, XL[i, j, o] > (sprintf("results/fw_%d.csv", o));
		}
	}
}

print F;
print OBJ_FUNC;
print F - OBJ_FUNC;
print GAP;
display ITERATION;

/*
FW:
0) Find x0 in P
1) ck = gradient f(xk)
   solve Min(y) ck'y, s.t y in P --> yh (Vg)
2) Evaluate relative gap
3) Line Search:Min(a) f(xk + a(yh - xk)), s.t. 0 <= a <= 1 (d = yh - xk) --> a*
4) xk+1 = xk + a*d

Min(x) f(x) s.t. x in P (Vnl)

TAP Objective function : integral[0, vij] sij(x) dx --> cij*x + DELTA/2 x^2
*/